\section{Source code for modelling}
    \vspace{2pt}
    \begin{boxminted}{bash}
        # set model windfarm
        set MaintenanceTypes;
        set MaintenanceSeverity;
        set StaffTypes;
        set StaffLevels;
        
        # cnt of maintenance required
        param main_req{mt in MaintenanceTypes, ms in MaintenanceSeverity};
        
        # cnt of staff required per maintenance type  
        param main_req_st{mt in MaintenanceTypes, st in StaffTypes};

        # required xp for maintenance task  
        param main_req_xp{mt in MaintenanceTypes, ms in MaintenanceSeverity}; 

        # maintenance cost  
        param main_material_cost{MaintenanceTypes}; 
            
        param staff_level_xp{StaffLevels};  
        param staff_cost{st in StaffTypes, sl in StaffLevels}; 	

        # weights to indicating staff's exhaustion due to work on task with 
        # given severity  
        param main_burnout{MaintenanceSeverity}; 

        # bearable level of staff's exhaustion  
        param burnout_coef; 
        
        ## variables
        # required XP points to carry out maintenance task
        var total_main_req_xp{MaintenanceTypes}; 

        # total available XP per staff types of hired personnel
        var total_staff_xp{StaffTypes};
        
        # total cnt of hired personnel
        var total_staff{StaffTypes}; 

        # XP of hired personnel per staff category
        var total_staff_xp_task{MaintenanceTypes}; 

        # nr of maintenance tasks weighted by burnout factor
        var weighted_maintenance_tasks{MaintenanceTypes}; 

        # total cnt of required staff for severity weighted maintenance tasks
        var total_req_wgt_staff{StaffTypes}; 
        
        var staff_to_hire{StaffTypes, StaffLevels} >=0, integer;
        var quantity{MaintenanceTypes} >= 0;
    \end{boxminted}
    \newline
    \newpage
    \vspace{2pt}
    \begin{boxminted}{bash}
        ## conditions
        # XP: sufficient XP points to carry out upcoming maintenance tasks 
        # based on severity
        s.t. RequiredXP{mt in MaintenanceTypes}: 
            total_main_req_xp[mt] = (if main_req[mt,"severe"] <> 0 then 
                                        main_req_xp[mt,"severe"] 
                                        else main_req_xp[mt,"normal"]);
        
        s.t. AvailableXP{st in StaffTypes}:
            total_staff_xp[st] = sum{sl in StaffLevels} staff_to_hire[st,sl] * 
                                    staff_level_xp[sl];
        
        s.t. AvailableXP_task{mt in MaintenanceTypes}:
            total_staff_xp_task[mt] = sum{st in StaffTypes} 
                                        if main_req_st[mt,st] <> 0 then 
                                        total_staff_xp[st] else 0 ;
            
        s.t. AvailableXP_ge_RequiredXP{mt in MaintenanceTypes}:
            total_staff_xp_task[mt] >= total_main_req_xp[mt];
        
        # staff: enough personnel to cover staff requirements per maintenance 
        # tasks	
        s.t. AvailableStaff{st in StaffTypes}:
            total_staff[st] = sum{sl in StaffLevels} staff_to_hire[st,sl];
            
        s.t. AvailableStaff_ge_RequiredStaff{mt in MaintenanceTypes, 
        st in StaffTypes}:
            total_staff[st] >= main_req_st[mt, st];
        
        # minimum maintenance
        s.t. RequiredMaintenanceDone{mt in MaintenanceTypes}: 
            quantity[mt] >= sum{ms in MaintenanceSeverity} main_req[mt,ms];
        
        # burnout indicators
        s.t. StaffNoBurnout{mt in MaintenanceTypes}:
            weighted_maintenance_tasks[mt] = sum{ms in MaintenanceSeverity} 
                main_req[mt,ms] * main_burnout[ms];
        
        s.t. TotalReqWgtStaff{st in StaffTypes}:
            total_req_wgt_staff[st] = sum{mt in MaintenanceTypes} 
                weighted_maintenance_tasks[mt] * main_req_st[mt,st];
            #total_req_wgt_staff[st] = sum{mt in MaintenanceTypes} 
                # weighted_maintenance_tasks[mt] * 
                # (if main_req_st[mt,st] == 0 then 0 else 1);
        
        # burnout
        s.t. BurnOutNotAllowed{st in StaffTypes}:
            total_req_wgt_staff[st] <= total_staff[st] * burnout_coef;
        
        ## Objective
        minimize TotalCosts: 
            sum{st in StaffTypes, sl in StaffLevels} staff_to_hire[st,sl] * 
                staff_cost[st,sl] +
            sum{mt in MaintenanceTypes} (main_material_cost[mt]) * 
                quantity[mt] ;
        
        solve;
        
    \end{boxminted}
    \newline

\section{Source code of maintenance data file}
    \vspace{2pt}
    \begin{boxminted}{bash}
        # set data maintenance
        data;

        set MaintenanceTypes :=
            blades
            gearbox
            generator
            sensors
            wiring;
        
        set MaintenanceSeverity :=
            normal
            severe;
        
        param main_req :
                    normal		severe :=
            blades 12 5
            gearbox 13 3
            generator 6 10
            sensors 8 1
            wiring 9 0;
        
        param main_req_xp :
                    normal severe :=
            blades 50 125
            gearbox 125 150
            generator 113 230
            sensors 110 150
            wiring 30 50;
        
        param main_req_st :
                    electric mechanic storage software :=
            blades 0 3 4 0
            gearbox 3 4 0 1
            generator 4 3 0 1
            sensors 5 3 1 3
            wiring 3 0 3 2;
        
        param main_burnout :=
            normal .75
            severe 1.3;
        
        param burnout_coef := 15 ;
        
        param main_material_cost :=
            blades 100
            gearbox 150
            generator 120
            sensors 50
            wiring 77;

        end;         
    \end{boxminted}
    \newline

\section{Source code of staff data file}
    \vspace{2pt}
    \begin{boxminted}{bash}
        # set data staff
        data;
        
        set StaffTypes :=
            electric
            mechanic
            storage
            software;
        
        set StaffLevels :=
            junior
            middle
            senior;
        
        param staff_level_xp :=
            junior 2
            middle 15
            senior 40;
        
        param staff_cost :
                    junior middle senior :=
            electric 10 15 40
            mechanic 8 18 30
            storage 12 15 20
            software 10 40 50;
        
        end;     
    \end{boxminted}
    \newline
